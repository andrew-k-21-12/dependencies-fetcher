apply plugin: 'maven-publish'
apply plugin: 'signing'

// Task to prepare a sources JAR to help end users get insights about the implementations.
task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.kotlin.srcDirs
}

// Task to prepare a documentations JAR.
task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

// Artifacts to be created.
artifacts {
    archives sourcesJar
    archives javadocJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {

                // Identifiers and version to label the publication.
                groupId    group
                artifactId artifactId
                version    version

                from components.java

                // Artifacts to be published.
                artifact sourcesJar
                artifact javadocJar

                pom {
                    name        = artifactId
                    description = 'A simple dependencies fetcher to be used inside of Gradle build scripts'
                    url         = 'https://github.com/andrew-k-21-12/dependencies-fetcher'
                    licenses {
                        license {
                            name = 'The MIT License'
                            url  = 'https://github.com/andrew-k-21-12/dependencies-fetcher/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id    = 'andrew-k-21-12'
                            name  = 'Andrey Koshmanskiy'
                            email = 'andrew.k.21.12@gmail.com'
                        }
                    }
                    scm {
                        connection          = 'scm:git:github.com/andrew-k-21-12/dependencies-fetcher.git'
                        developerConnection = 'scm:git:ssh://github.com/andrew-k-21-12/dependencies-fetcher.git'
                        url                 = 'https://github.com/andrew-k-21-12/dependencies-fetcher/tree/master'
                    }
                }

            }
        }
    }
}

signing {
    sign publishing.publications
}
